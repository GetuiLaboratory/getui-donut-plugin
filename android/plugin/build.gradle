buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.github.kezong:fat-aar:1.3.8'
    }
}

//TODO：使用FCM推送待配置项
//添加插件 com.google.gms.google-services ' ==>  参考：https://developers.weixin.qq.com/miniprogram/dev/platform-capabilities/miniapp/handbook/plugin/androidPlugin.html
//添加google-services.json文件  ==>   参考： https://developers.weixin.qq.com/miniprogram/dev/platform-capabilities/miniapp/handbook/devtools/android-native-resource.html


apply from: rootProject.file('gradle/build_library.gradle')
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.kezong.fat-aar'


android {

    def miniAppPluginConfig = rootProject.ext.miniAppPluginConfig
    defaultConfig {
        buildConfigField "String", "PLUGIN_ID", "\"$miniAppPluginConfig.pluginId\""

    }

}


repositories {
    flatDir {
        dirs 'libs'
    }

    maven {
        url "https://maven.oa.com/nexus/content/repositories/google-maven/"
    }
}

def saaaSDKVersionDepend = rootProject.ext.saaaSDKVersionDepend

def libsDir = rootProject.file('libs')

if (!file("$libsDir/com/tencent/luggage/wxa-standalone-open-runtime-SaaA-plugin-sdk/${saaaSDKVersionDepend}").exists()) {
    libsDir.mkdir()

    apply plugin: 'de.undercouch.download'

    def zipFileName = "wxa-standalone-open-runtime-SaaA-sdk-all-${saaaSDKVersionDepend}.zip"
    download.run {
        src "https://dldir1.qq.com/WechatWebDev/donut/android/pluginsdk/${zipFileName}"
        dest libsDir
    }
    def zipFilePath = "$libsDir/$zipFileName"
    copy {
        def subDir = 'localAar'
        from {
            zipTree(zipFilePath).matching { include "$subDir/**" }
        }
        into libsDir
        eachFile { fcp ->
            fcp.path = fcp.path.replaceFirst("^$subDir", '')
        }
        includeEmptyDirs false
    }
    file(zipFilePath).delete()
}


fataar {
    /**
     * 如果你想将所有远程依赖在pom中声明的依赖项同时打入在最终产物里的话，
     * 你需要在build.gradle中将transitive值改为true
     * @since 1.3.0
     */
    transitive = false
}

repositories {
    maven {
        url "https://mvn.getui.com/nexus/content/repositories/releases/"
    }
}
dependencies {
    implementation "androidx.exifinterface:exifinterface:1.1.0"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    api "androidx.constraintlayout:constraintlayout:1.1.3"

    api 'com.google.android.material:material:1.0.0'

    compileOnly("com.tencent.luggage:wxa-standalone-open-runtime-SaaA-plugin-sdk:$saaaSDKVersionDepend") {}
    embed 'com.getui:sdk-for-gj:4.4.3.15'
    embed 'com.getui.opt:fcm:3.1.2'
    implementation 'com.google.firebase:firebase-messaging:23.0.0'
    implementation 'com.google.firebase:firebase-analytics:20.1.0'

}

clearDuplicatedJniLibs([
        '**/jni/x86/**',
        '**/jni/x86_64/**',
        '**/jni/armeabi/**',
        '**/jni/**/libc++_shared.so'
])

project.ext.pomDeps = [
        "com.google.firebase:firebase-messaging": "23.0.0",
        "com.google.firebase:firebase-analytics": "20.1.0"
]



